PREFIX  rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX  owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX     : <http://www.semanticweb.org/guerrero/ontologies/2021/10/project-ontology#>

# 1. Quais os títulos dos filmes que foram dirigidos pelo diretor D, em ordem lexicográfica?
SELECT ?titulo
WHERE {
      ?filme :titulo ?titulo.
      ?filme :produzidoPor :brian_de_palma
      }
ORDER BY ?titulo

# 2. Quais os primeiros nomes e últimos nomes dos atores que participaram do filme de título Ft , em ordem lexicográfica de nome e sobrenome, com seus respectivos personagens?
SELECT ?nome ?sobrenome ?personagem
WHERE {
      ?ator foaf:firstName ?nome;
            foaf:familyName ?sobrenome;
            :atuouPersonagemEm ?filme;
            :atuou ?personagem.
      ?personagem :participouEm ?filme.
      ?filme :titulo "Crash"
      }
ORDER BY ?nome ?sobrenome

# 3. Em quais filmes os atores X e Y atuaram juntos, com os respectivos diretores e anos de lançamento, do mais novo para o mais antigo?
SELECT ?filme ?diretor ?ano
WHERE {
      ?filme :anoDeLancamento ?ano;
             :produzidoPor ?diretor;
             :temPersonagemAtuadoPor :sofia_coppola;
             :temPersonagemAtuadoPor :gian_carlo_coppola.
      }
ORDER BY ?ano

# 4. Quais filmes do diretor do filme F possuem X ou Y como atores, com as respectivas durações em ordem crescente?
SELECT ?filme ?duracao
WHERE {
        {
          ?diretor :produziu :filme_the_godfather;
                   :produziu ?filme.
          ?filme :duracao ?duracao;
                 :temPersonagemAtuadoPor :sofia_coppola.
        }
        UNION
        {
          ?diretor :produziu :filme_anna;
                   :produziu ?filme.
          ?filme :duracao ?duracao;
                 :temPersonagemAtuadoPor :gian_carlo_coppola.
        }
      }
GROUP BY ?filme ?duracao
ORDER BY ?duracao

# 5. Quais pessoas atuaram em um filme e dirigiram algum filme (não necessariamente o mesmo filme, mas obrigatoriamente a mesma pessoa)?
SELECT ?pessoa ?filmeAtuou ?filmeDirigiu
WHERE {
      ?pessoa :atuouPersonagemEm ?filmeAtuou; 
              :produziu ?filmeDirigiu
      }
ORDER BY ?pessoa

# 6. Quais diretores dirigiram algum filme em um ano entre os anos N1 e N2, em que os atores X e Y aparecem, do mais antigo para o mais novo?
SELECT ?diretor ?filme ?ano
WHERE {
      ?diretor :produziu ?filme.
      ?filme :anoDeLancamento ?ano;
             :temPersonagemAtuadoPor :harrison_ford;
             :temPersonagemAtuadoPor :morgan_paull
      FILTER(?ano >= 1980 && ?ano <= 1990)
      }
ORDER BY ?ano

# 7. Quais pares formados por uma uma atriz e um ator atuaram juntos em algum filme de duração entre M1 e M2 ?
SELECT ?atriz ?ator ?filme ?duracao
WHERE {
      ?filme :duracao ?duracao;
             :temPersonagemAtuadoPor ?atriz;
             :temPersonagemAtuadoPor ?ator.
      ?ator foaf:gender "Masculino"
      FILTER(?duracao >= 90 && ?duracao <= 100)
      }
ORDER BY ?filme

# 8. Qual o diretor que mais dirigiu filmes em que aparece o ator de primeiro nome Xp e último nome Xu ?
SELECT  ?diretor
WHERE {
      {
      SELECT ?diretor (COUNT(?filme_) as ?count)
      WHERE {
            ?diretor :produziu ?filme_.
            ?filme_ :temPersonagemAtuadoPor ?ator_.
            ?ator_ foaf:firstName "Billy";
                  foaf:familyName "Thornton".
            }
      GROUP BY ?diretor
      }
      .
      { 
      SELECT (MAX(?filmes) AS ?max)
            WHERE
            { 
            SELECT  ?diretor (COUNT(?filme) AS ?filmes)
            WHERE {
                  ?diretor :produziu ?filme.
                  ?filme :temPersonagemAtuadoPor ?ator.
                  ?ator foaf:firstName "Billy";
                        foaf:familyName "Thornton"
                  }
            GROUP BY ?diretor
            }
      }
      FILTER(?count = ?max)
      }
LIMIT 1

# 9. Qual o filme mais antigo em que o ator X atuou, em que ano foi lançado e qual era seu personagem? Havendo empate, devolver todos do mesmo ano.
SELECT  ?filme ?ano ?personagem
WHERE {
      ?filme :compostoPor ?personagem;
             :anoDeLancamento ?ano.
      :billy_bob_thornton :atuou ?personagem.
      {
      SELECT (MIN(?ano) as ?min)
      WHERE {
            ?filme :compostoPor ?personagem;
                   :anoDeLancamento ?ano.
            :billy_bob_thornton :atuou ?personagem.
            }
      }
      FILTER(?ano = ?min)
      }

# 10. Qual o filme mais longo dirigido por D, e qual a sua duração? Havendo empate, devolver todos da mesma duração.
SELECT  ?filme ?duracao
WHERE {
      ?filme :produzidoPor :francis_ford_coppola;
                  :duracao ?duracao.
      {
      SELECT (MAX(?duracao) as ?max)
      WHERE {
                  ?filme :produzidoPor :francis_ford_coppola;
                        :duracao ?duracao.
            }
      }
      FILTER(?duracao = ?max)
      }